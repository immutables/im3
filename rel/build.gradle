
def javaVersion = 17

group publishGroup
version publishVersion

subprojects {
	apply plugin: 'java-library' // gives api/implementation scopes
	apply plugin: 'signing'
	apply plugin: 'maven-publish'

	repositories {
		mavenCentral()
	}

	group = rootProject.group
	version = rootProject.version

	sourceCompatibility = javaVersion
	targetCompatibility = javaVersion

	java {
		toolchain.languageVersion = JavaLanguageVersion.of(javaVersion)
		modularity.inferModulePath = true
		withJavadocJar()
		withSourcesJar()
	}

	compileJava {
		options.compilerArgs += '--enable-preview'
	}

	tasks.withType(JavaCompile).configureEach {
		options.compilerArgs += '--enable-preview'
		options.compilerArgs += '-parameters'
		options.incremental = true
	}

	tasks.withType(Test).configureEach {
		jvmArgs += '--enable-preview'
	}

	tasks.withType(JavaExec).configureEach {
		jvmArgs += '--enable-preview'
	}

	tasks.withType(Javadoc).configureEach {
		def javadocOptions = options as CoreJavadocOptions

		javadocOptions.addStringOption('-release', "$javaVersion")
		javadocOptions.addBooleanOption('-enable-preview', true)
	}

	copy {
		duplicatesStrategy = DuplicatesStrategy.INCLUDE
	}

	publishing {
		publications {
			mavenJava(MavenPublication) {
				artifactId = project.name
				from components.java

				versionMapping {
					usage('java-api') {
						fromResolutionOf('runtimeClasspath')
					}
					usage('java-runtime') {
						fromResolutionResult()
					}
				}
				pom {
					name = group + '.' + artifactId
					description = 'A concise description of my library'
					url = 'http://immutables.org'
					properties = [:]
					licenses {
						license {
							name = 'The Apache Software License, Version 2.0'
							url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						}
					}
					developers {
						developer {
							id = 'elucash'
							name = 'Ievgen Lukash'
							email = 'e.lucash@gmail.com'
						}
					}
					scm {
						connection = 'scm:git:https://github.com/immutables/im3.git'
						developerConnection = 'scm:git:git@github.com:immutables/im3.git'
						url = 'https://github.com/immutables/im3.git'
					}
				}
			}
		}
		repositories {
			maven {
				url 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
				credentials {
					username sonatypeUsername
					password sonatypePassword
				}
			}
		}
	}

	signing {
		sign publishing.publications.mavenJava
	}
}
